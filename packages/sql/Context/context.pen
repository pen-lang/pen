import 'Pool'Options { Options }
import 'Pool'pool { Pool }

import foreign _pen_sql_pool_create \(string, Options) poolResult
import foreign _pen_sql_pool_query \(Pool, string, [any]) listResult
import foreign _pen_sql_pool_execute \(Pool, string, [any]) numberResult

type poolResult {
  pool Pool
  error_ string
}

type listResult {
  list [any]
  error_ string
}

type numberResult {
  value number
  error_ string
}

type Context {
  inner Inner
}

type Inner {
  CreatePool \(string, Options) Pool | error
  QueryPool \(Pool, string, [any]) [[any]] | error
  ExecutePool \(Pool, string, [any]) number | error
}

New = \() Context {
  Context{
    inner: Inner{
      CreatePool: \(uri string, options Options) Pool | error {
        r = _pen_sql_pool_create(uri, options)

        if r.error_ == "" {
          r.pool
        } else {
          error(r.error_)
        }
      },
      QueryPool: \(pool Pool, query string, arguments [any]) [[any]] | error {
        r = _pen_sql_pool_query(pool, query, arguments)

        if r.error_ == "" {
          [[any] if r = r() as [any] { r } else { [any] } for r in r.list]
        } else {
          error(r.error_)
        }
      },
      ExecutePool: \(pool Pool, query string, arguments [any]) number | error {
        r = _pen_sql_pool_execute(pool, query, arguments)

        if r.error_ == "" {
          r.value
        } else {
          error(r.error_)
        }
      },
    },
  }
}

Inner = \(ctx Context) Inner {
  ctx.inner
}

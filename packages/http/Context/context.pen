import 'Context'ffiHeaderMap { FfiHeaderMap }
import 'Context'ffiResponse { FfiResponse }
import 'Request { Request }
import 'Response { Response }

import foreign _pen_http_client_send \(string, string, FfiHeaderMap, string) ffiResponseResult
import foreign _pen_http_server_serve \(string, \(string, string, FfiHeaderMap, string) FfiResponse) string

type Context {
  inner Inner
}

type Inner {
  Send \(Request) Response | error
  Serve \(string, \(Request) Response) none | error
}

type ffiResponseResult {
  response FfiResponse
  error_ string
}

New = \() Context {
  Context{
    inner: Inner{
      Send: \(r Request) Response | error {
        r = _pen_http_client_send(
          r.Method,
          r.Uri,
          ffiHeaderMap'New(r.Headers),
          r.Body,
        )

        if r.error_ == "" {
          r = r.response

          Response{
            Status: r.Status,
            Headers: ffiHeaderMap'ToMap(r.Headers),
            Body: r.Body,
          }
        } else {
          error(r.error_)
        }
      },
      Serve: \(address string, callback \(Request) Response) none | error {
        e = _pen_http_server_serve(
          address,
          \(m string, u string, hs FfiHeaderMap, b string) FfiResponse {
            r = callback(
              Request{
                Method: m,
                Uri: u,
                Headers: ffiHeaderMap'ToMap(hs),
                Body: b,
              },
            )

            FfiResponse{
              Status: r.Status,
              Headers: ffiHeaderMap'New(r.Headers),
              Body: r.Body,
            }
          },
        )
        if e == "" { none } else { error(e) }
      },
    },
  }
}

Inner = \(ctx Context) Inner {
  ctx.inner
}

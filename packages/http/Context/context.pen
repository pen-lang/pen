import 'Context'ffiHeaderMap { FfiHeaderMap }
import 'Context'ffiResponse { FfiResponse }
import 'Request { Request }
import 'Response { Response }

import foreign _pen_http_client_send \(string, string, FfiHeaderMap, string) FfiResponse | error
import foreign _pen_http_server_serve \(string, \(string, string, FfiHeaderMap, string) FfiResponse) none | error

type Context {
  inner Inner
}

type Inner {
  Send \(Request) Response | error
  Serve \(string, \(Request) Response) none | error
}

New = \() Context {
  Context{
    inner: Inner{
      Send: \(r Request) Response | error {
        r = _pen_http_client_send(
          r.Method,
          r.Uri,
          ffiHeaderMap'New(r.Headers),
          r.Body,
        )?

        Response{
          Status: r.Status,
          Headers: ffiHeaderMap'ToMap(r.Headers),
          Body: r.Body,
        }
      },
      Serve: \(address string, callback \(Request) Response) none | error {
        _pen_http_server_serve(
          address,
          \(m string, u string, hs FfiHeaderMap, b string) FfiResponse {
            r = callback(
              Request{
                Method: m,
                Uri: u,
                Headers: ffiHeaderMap'ToMap(hs),
                Body: b,
              },
            )

            FfiResponse{
              Status: r.Status,
              Headers: ffiHeaderMap'New(r.Headers),
              Body: r.Body,
            }
          },
        )
      },
    },
  }
}

Inner = \(ctx Context) Inner {
  ctx.inner
}

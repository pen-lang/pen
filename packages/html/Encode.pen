import Core'String

import 'Node { Attribute, Node, Tag }

Encode = \(n Node) string | error {
  if n = n as string {
    n
  } else {
    attrs = encodeAttributes(n.Attributes)

    ns = if size(n.Children) == 0 {
      [string "/>"]
    } else {
      ns = encodeChildren(n.Children)?

      [string ">", ...ns, "</", n.Tag, ">"]
    }

    String'Concatenate(
      [string
        "<",
        n.Tag,
        ...if size(attrs) == 0 {
          [string]
        } else {
          [string " ", ...attrs]
        },
        ...ns,
      ],
    )
  }
}

encodeAttributes = \(attrs [Attribute]) [string] {
  [string String'Concatenate([string a().Key, "=\"", a().Value, "\""]) for a in attrs]
}

encodeChildren = \(ns [Node]) [string] | error {
  if [n, ...ns] = ns {
    n = Encode(n())?
    ns = encodeChildren(ns)?

    [string n, ...ns]
  } else {
    [string]
  }
}

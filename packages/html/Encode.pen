import Core'String

import 'Node { Node, Tag }

tagToString = \() {Tag: string} {
  {Tag: string
    Node'A{}: "a",
    Node'Body{}: "body",
    Node'Div{}: "div",
    Node'Head{}: "head",
    Node'Html{}: "html",
    Node'P{}: "p",
    Node'Title{}: "title",
  }
}

Encode = \(n Node) string | error {
  tag = if s = tagToString()[n.Tag] {
    s
  } else {
    error("unknown tag")
  }?

  attrs = encodeAttributes(n.Attributes)

  ns = if size(n.Children) == 0 {
    [string "/>"]
  } else {
    ns = encodeChildren(n.Children)?

    [string ">", ...ns, "</", tag, ">"]
  }

  String'Concatenate(
    [string
      "<",
      tag,
      " ",
      ...attrs,
      ...ns,
    ],
  )
}

encodeAttributes = \(attrs {string: string}) [string] {
  [string String'Concatenate([string k, "=\"", v, "\""]) for k, v in attrs]
}

encodeChildren = \(ns [Node]) [string] | error {
  if [n, ...ns] = ns {
    n = Encode(n())?
    ns = encodeChildren(ns)?

    [string n, ...ns]
  } else {
    [string]
  }
}

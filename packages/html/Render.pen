import Core'String

import 'Node { Attribute, Node, Tag }

import foreign "c" _pen_html_encode_attribute \(string) string
import foreign "c" _pen_html_encode_text \(string) string

# Render an HTML node.
Render = \(n Node) string | error {
  if n = n as string {
    _pen_html_encode_text(n)
  } else {
    attrs = renderAttributes(n.Attributes)

    ns = if size(n.Children) == 0 {
      [string "/>"]
    } else {
      ns = renderChildren(n.Children)?

      [string ">", ...ns, "</", n.Tag, ">"]
    }

    String'Concatenate(
      [string
        "<",
        # TODO Validate tag names.
        n.Tag,
        ...if size(attrs) == 0 {
          [string]
        } else {
          [string " ", ...attrs]
        },
        ...ns,
      ],
    )
  }
}

renderAttributes = \(attrs [Attribute]) [string] {
  [string
    # TODO Validate attribute keys.
    String'Concatenate([string a().Key, "=\"", _pen_html_encode_attribute(a().Value), "\""])
    for a in attrs
  ]
}

renderChildren = \(ns [Node]) [string] | error {
  if [n, ...ns] = ns {
    n = Render(n())?
    ns = renderChildren(ns)?

    [string n, ...ns]
  } else {
    [string]
  }
}

import Console'Print
import Core'List
import Core'Number
import Core'String
import Os'Context { Context }
import Os'File
import Os'Process
import Os'Random
import Os'Time

import 'lifeGame

rows = \() number { 20 }

columns = \() number { 40 }

main = \(ctx context) none {
  if e = run(ctx.Os) as none {
    none
  } else {
    _ = Print'Line(ctx.Os, if s = source(e) as string { s } else { "unknown error" })
    Process'Exit(ctx.Os, 1)
  }
}

run = \(ctx Context) none | error {
  step(
    ctx,
    lifeGame'Initialize(
      lifeGame'NewContext(\() number { Random'Number(ctx) }),
      rows(),
      columns(),
    ),
  )
}

step = \(ctx Context, rows [[boolean]]) none | error {
  File'Write(ctx, File'StdOut(), "\x1bc")?
  File'Write(ctx, File'StdOut(), lifeGame'Render(rows))?

  step(ctx, lifeGame'Step(rows)?)
}

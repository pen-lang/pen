import Console'Print
import Os'Context { Context }
import Os'File { File }
import Os'Environment
import Os'Process

bufferSize = \() number { 4096 }

main = \(ctx Context) none {
  if e = run(ctx) as none {
    none
  } else {
    Print'Line(ctx, if s = source(e) as string { s } else { "unknown error" })
    Process'Exit(ctx, 1)
  }
}

run = \(ctx Context) none | error {
  fs = Environment'Arguments(ctx)

  if [_, ..._] = fs {
    concatenateFiles(ctx, fs)
  } else {
    pipeFile(ctx, File'StdIn())
  }
}

concatenateFiles = \(ctx Context, fs [string]) none | error {
  if [f, ...fs] = fs {
    pipeFile(ctx, File'Open(ctx, f())?)?
    concatenateFiles(ctx, fs)
  } else {
    none
  }
}

pipeFile = \(ctx Context, f File) none | error {
  s = File'ReadLimit(ctx, f, bufferSize())?

  if s == "" {
    none
  } else {
    File'Write(ctx, File'StdOut(), s)?
    pipeFile(ctx, f)
  }
}

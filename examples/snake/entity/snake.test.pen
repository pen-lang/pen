import Test'Assert

import 'direction
import 'entity'snake
import 'field { Field }
import 'position { Position }

Head = \() none | error {
  p = Position{X: 1, Y: 1}

  Assert'True(snake'Head(snake'New(p)) == p)
}

Positions = \() none | error {
  p = Position{X: 1, Y: 1}

  Assert'True(snake'Positions(snake'New(p)) == [Position p])
}

PositionsWithTick = \() none | error {
  p = Position{X: 1, Y: 1}

  Assert'True(snake'Positions(snake'Tick(snake'New(p))) == [Position p])
}

MoveHead = \() none | error {
  Assert'True(
    snake'Head(snake'Tick(snake'Move(snake'New(Position{X: 1, Y: 1}), direction'Up{})))
      == Position{X: 1, Y: 0},
  )
}

Move = \() none | error {
  Assert'True(
    snake'Positions(snake'Tick(snake'Move(snake'New(Position{X: 1, Y: 1}), direction'Up{})))
      == [Position Position{X: 1, Y: 0}],
  )
}

Grow = \() none | error {
  p = Position{X: 1, Y: 1}

  Assert'True(
    snake'Positions(snake'Tick(snake'Move(snake'Grow(snake'New(p)), direction'Up{})))
      == [Position Position{X: 1, Y: 0}, p],
  )
}

IsCrashed = \() none | error {
  f = Field{Width: 1, Height: 1}
  s = snake'New(Position{X: 1, Y: 1})

  Assert'True(!snake'IsCrashed(s, f))?
  Assert'True(snake'IsCrashed(snake'Tick(snake'Move(s, direction'Up{})), f))
}

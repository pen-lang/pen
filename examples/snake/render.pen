import Core'Number
import Core'String

import 'creature { Creature }
import 'creature'frog { Frog }
import 'creature'snake { Snake }
import 'field { Field }
import 'game { Game }
import 'position { Position }
import 'usage { Usage }

empty = \() string { "." }

Render = \(g Game) string {
  f = game'Field(g)

  String'Concatenate(
    [string
      "\x1bc",
      String'Join(
        [string
          String'Concatenate(
            [string
              if c = field'Get(f, Position{X: x(), Y: y()}) as none {
                empty()
              } else {
                renderCreature(c)
              }
              for y in Number'Sequence(field'Columns(f))
            ],
          )
          for x in Number'Sequence(field'Rows(f))
        ],
        "\r\n",
      ),
      "\r\n",
      Usage(),
      "\r\n",
    ],
  )
}

renderCreature = \(c Creature) string {
  if _ = c as Snake {
    "o"
  } else if Frog {
    "*"
  }
}

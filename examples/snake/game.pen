import Core'Number

import 'command { Command }
import 'creature { Creature }
import 'creature'frog
import 'creature'snake { Snake }
import 'field { Field }
import 'position { Position }

type Game {
  snake Snake
  field Field
  entities {Position: Creature}
}

New = \(c number, r number) Game {
  p = Position{X: Number'Floor(c / 2), Y: Number'Floor(r / 2)}
  s = snake'New(p)

  Game{
    snake: s,
    field: Field{Columns: c, Rows: r},
    entities: {Position: Creature p: s},
  }
}

Field = \(g Game) Field { g.field }

RunCommand = \(g Game, c Command) Game {
  s = snake'Move(g.snake, c)

  Game{...g, snake: s, entities: putSnake({Position: Creature}, s, snake'Positions(s))}
}

putSnake = \(es {Position: Creature}, s Snake, ps [Position]) {Position: Creature} {
  if [p, ...ps] = ps {
    putSnake({Position: Creature ...es, p(): s}, s, ps)
  } else {
    es
  }
}

EntityAt = \(g Game, p Position) Creature | none {
  if c = g.entities[p] {
    c
  } else {
    none
  }
}

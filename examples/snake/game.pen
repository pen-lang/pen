import Core'Number

import 'direction { Direction }
import 'entity { Entity }
import 'entity'frog
import 'entity'snake { Snake }
import 'field { Field }
import 'position { Position }

type Game {
  snake Snake
  field Field
  entities {Position: Entity}
}

New = \(f Field) Game {
  p = Position{X: Number'Floor(f.Columns / 2), Y: Number'Floor(f.Rows / 2)}
  s = snake'New(p)

  Game{
    snake: s,
    field: f,
    entities: {Position: Entity p: s},
  }
}

Field = \(g Game) Field { g.field }

MoveSnake = \(g Game, d Direction) Game {
  Game{...g, snake: snake'Move(g.snake, d)}
}

Tick = \(g Game) Game | none {
  if s = snake'Tick(g.snake, g.field) as none {
    none
  } else {
    Game{...g, snake: s, entities: putSnake({Position: Entity}, s, snake'Positions(s))}
  }
}

putSnake = \(es {Position: Entity}, s Snake, ps [Position]) {Position: Entity} {
  if [p, ...ps] = ps {
    putSnake({Position: Entity ...es, p(): s}, s, ps)
  } else {
    es
  }
}

EntityAt = \(g Game, p Position) Entity | none {
  if e = g.entities[p] {
    e
  } else {
    none
  }
}

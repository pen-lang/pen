import Core'Number

import 'command { Command }
import 'entity { Entity }
import 'entity'frog
import 'entity'snake { Snake }
import 'field { Field }
import 'position { Position }

type Game {
  snake Snake
  field Field
  entities {Position: Entity}
}

New = \(c number, r number) Game {
  p = Position{X: Number'Floor(c / 2), Y: Number'Floor(r / 2)}
  s = snake'New(p)

  Game{
    snake: s,
    field: Field{Columns: c, Rows: r},
    entities: {Position: Entity p: s},
  }
}

Field = \(g Game) Field { g.field }

RunCommand = \(g Game, c Command) Game | none {
  if s = snake'Move(g.snake, c, g.field) as none {
    none
  } else {
    Game{...g, snake: s, entities: putSnake({Position: Entity}, s, snake'Positions(s))}
  }
}

putSnake = \(es {Position: Entity}, s Snake, ps [Position]) {Position: Entity} {
  if [p, ...ps] = ps {
    putSnake({Position: Entity ...es, p(): s}, s, ps)
  } else {
    es
  }
}

EntityAt = \(g Game, p Position) Entity | none {
  if e = g.entities[p] {
    e
  } else {
    none
  }
}

import 'Request { Request }
import 'Response { Response }

import foreign _pen_http_client_send \(Request) FfiResponse
import foreign _pen_http_server_serve \(string, \(Request) Response) string

type FfiResponse {
  response Response
  error string
}

type Context {
  inner Inner
}

type Inner {
  Send \(Request) Response | error
  Serve \(string, \(Request) Response) none | error
}

New = \() Context {
  Context{
    inner: Inner{
      Send: \(r Request) Response | error {
        r = _pen_http_client_send(r)

        if r.error == "" {
          r.response
        } else {
          error(r.error)
        }
      },
      Serve: \(address string, callback \(Request) Response) none | error {
        e = _pen_http_server_serve(address, callback)

        if e == "" { none } else { error(e) }
      },
    },
  }
}

Inner = \(ctx Context) Inner {
  ctx.inner
}

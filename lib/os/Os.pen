import foreign "c" _pen_os_fd_read \(number, number) fdReadResult
import foreign "c" _pen_os_fd_write \(number, string) fdWriteResult
import foreign "c" _pen_os_open_file \(string, OpenFileOptions) openFileResult
import foreign "c" _pen_os_write_file \(any, string) writeFileResult
import foreign _pen_main \(Os) number

type Os {
  fdRead \(number, number) fdReadResult,
  fdWrite \(number, string) fdWriteResult,
  openFile \(string, OpenFileOptions) openFileResult,
  writeFile \(any, string) writeFileResult,
}

type File {
  internal any,
}

type fdReadResult {
  bytes string,
  error number,
}

type fdWriteResult {
  bytes number,
  error number,
}

type openFileResult {
  file any,
  error number,
}

type writeFileResult {
  _ none,
  error number,
}

type OpenFileOptions {
  Read boolean,
  Write boolean,
}

DefaultOpenFileOptions = \() OpenFileOptions {
  OpenFileOptions{
    Read: false,
    Write: false,
  }
}

FdRead = \(os Os, fd number, count number) string | error {
  r = os.fdRead(fd, count)

  if r.error == -1 {
    r.bytes
  } else {
    error(r.error)
  }
}

FdWrite = \(os Os, fd number, data string) number | error {
  r = os.fdWrite(fd, data)

  if r.error == -1 {
    r.bytes
  } else {
    error(r.error)
  }
}

StdIn = \() number { 0 }

StdOut = \() number { 1 }

StdErr = \() number { 2 }

OpenFile = \(os Os, path string, opts OpenFileOptions) File | error {
  r = os.openFile(path, opts)

  if r.error == -1 {
    File{internal: r.file}
  } else {
    error(r.error)
  }
}

WriteFile = \(os Os, file File, data string) none | error {
  r = os.writeFile(file.internal, data)

  if r.error == -1 {
    none
  } else {
    error(r.error)
  }
}

export foreign _pen_os_main = \(_ none) number {
  _pen_main(
    Os{
      fdRead: _pen_os_fd_read,
      fdWrite: _pen_os_fd_write,
      openFile: _pen_os_open_file,
      writeFile: _pen_os_write_file,
    },
  )
}

import 'array
import 'Os'context
import 'Os'errorTranslator
import 'Os'normalFile
import 'Os'openFileOptions

import foreign _pen_main \(context'Context) number

type File {
  inner normalFile'NormalFile | specialFile
}

type stdIn {}
type stdOut {}
type stdErr {}

type specialFile = stdIn | stdOut | stdErr

type Context = context'Context

type OpenFileOptions = openFileOptions'OpenFileOptions

StdIn = \() File {
  File{inner: stdIn}
}

StdOut = \() File {
  File{inner: stdOut}
}

StdErr = \() File {
  File{inner: stdErr}
}

DefaultOpenFileOptions = \() openFileOptions'OpenFileOptions {
  openFileOptions'Default()
}

OpenFileWithOptions = \(ctx context'Context, path string, opts openFileOptions'OpenFileOptions) File | error {
  r = context'Inner(ctx).OpenFile(path, opts)

  if r.Error == 0 {
    File{inner: r.File}
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

OpenFile = \(ctx context'Context, path string) File | error {
  OpenFileWithOptions(
    ctx,
    path,
    openFileOptions'OpenFileOptions{...openFileOptions'Default(), Read: true}
  )
}

ReadFile = \(ctx context'Context, file File) string | error {
  r = if f = file.inner as stdIn {
    context'Inner(ctx).ReadStdIn()
  } else if stdOut {
    error("cannot read from stdout")
  } else if stdErr {
    error("cannot read from stderr")
  } else if normalFile'NormalFile {
    context'Inner(ctx).ReadFile(f)
  }?

  if r.Error == 0 {
    r.Value
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

WriteFile = \(ctx context'Context, file File, data string) number | error {
  inner = context'Inner(ctx)

  r = if f = file.inner as stdIn {
    error("cannot write to stdin")
  } else if stdOut {
    inner.WriteStdOut(data)
  } else if stdErr {
    inner.WriteStdErr(data)
  } else {
    inner.WriteFile(f, data)
  }?

  if r.Error == 0 {
    r.Count
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

CopyFile = \(ctx context'Context, src string, dest string) none | error {
  r = context'Inner(ctx).CopyFile(src, dest)

  if r.Error == 0 {
    none
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

RemoveFile = \(ctx context'Context, path string) none | error {
  r = context'Inner(ctx).RemoveFile(path)

  if r.Error == 0 {
    none
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

ReadDirectory = \(ctx context'Context, path string) [string] | error {
  r = context'Inner(ctx).ReadDirectory(path)

  if r.Error == 0 {
    array'ConvertArrayToStrings(r.Paths)
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

CreateDirectory = \(ctx context'Context, path string) none | error {
  r = context'Inner(ctx).CreateDirectory(path)

  if r.Error == 0 {
    none
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

RemoveDirectory = \(ctx context'Context, path string) none | error {
  r = context'Inner(ctx).RemoveDirectory(path)

  if r.Error == 0 {
    none
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

Arguments = \(ctx context'Context) [string] {
  array'ConvertArrayToStrings(context'Inner(ctx).GetArguments())
}

EnvironmentVariable = \(ctx context'Context, name string) string | error {
  r = context'Inner(ctx).GetEnvironmentVariable(name)

  if r.Error == 0 {
    r.Value
  } else {
    error(errorTranslator'Translate(r.Error))
  }
}

export foreign _pen_os_main = \() number {
  _pen_main(context'New())
}

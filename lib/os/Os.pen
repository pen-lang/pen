import foreign "c" _pen_os_read_stdin \() ffiStringResult
import foreign "c" _pen_os_write_stdout \(string) ffiWriteResult
import foreign "c" _pen_os_write_stderr \(string) ffiWriteResult
import foreign "c" _pen_os_open_file \(string, OpenFileOptions) ffiOpenResult
import foreign "c" _pen_os_read_file \(file) ffiStringResult
import foreign "c" _pen_os_write_file \(file, string) ffiWriteResult
import foreign "c" _pen_os_copy_file \(string, string) ffiNoneResult
import foreign "c" _pen_os_remove_file \(string) ffiNoneResult
import foreign "c" _pen_os_read_directory \(string) ffiReadDirectoryResult
import foreign "c" _pen_os_create_directory \(string) ffiNoneResult
import foreign "c" _pen_os_remove_directory \(string) ffiNoneResult
import foreign "c" _pen_os_get_arguments \() array
import foreign "c" _pen_os_get_environment_variable \(string) ffiStringResult
import foreign "c" _pen_ffi_array_get \(array, number) any
import foreign "c" _pen_ffi_array_length \(array) number
import foreign "c" _pen_ffi_any_to_string \(any) string
import foreign _pen_main \(Os) number

type Os {
  readStdIn \() ffiStringResult
  writeStdOut \(string) ffiWriteResult
  writeStdErr \(string) ffiWriteResult
  openFile \(string, OpenFileOptions) ffiOpenResult
  readFile \(file) ffiStringResult
  writeFile \(file, string) ffiWriteResult
  copyFile \(string, string) ffiNoneResult
  removeFile \(string) ffiNoneResult
  readDirectory \(string) ffiReadDirectoryResult
  createDirectory \(string) ffiNoneResult
  removeDirectory \(string) ffiNoneResult
  getArguments \() array
  getEnvironmentVariable \(string) ffiStringResult
}

type File {
  inner file | specialFile
}

type file {
  inner any
}

type stdIn {}
type stdOut {}
type stdErr {}

type ffiOpenResult {
  file file
  error number
}

type ffiStringResult {
  value string
  error number
}

type ffiWriteResult {
  count number
  error number
}

type ffiNoneResult {
  _ none
  error number
}

type ffiReadDirectoryResult {
  paths array
  error number
}

type OpenFileOptions {
  Append boolean
  Create boolean
  CreateNew boolean
  Read boolean
  Truncate boolean
  Write boolean
}

type array {
  array any
}

type specialFile = stdIn | stdOut | stdErr

StdIn = \() File {
  File{inner: stdIn}
}

StdOut = \() File {
  File{inner: stdOut}
}

StdErr = \() File {
  File{inner: stdErr}
}

DefaultOpenFileOptions = \() OpenFileOptions {
  OpenFileOptions{
    Append: false,
    Create: false,
    CreateNew: false,
    Read: false,
    Truncate: false,
    Write: false,
  }
}

OpenFileWithOptions = \(os Os, path string, opts OpenFileOptions) File | error {
  r = os.openFile(path, opts)

  if r.error == 0 {
    File{inner: r.file}
  } else {
    error(translateError(r.error))
  }
}

OpenFile = \(os Os, path string) File | error {
  OpenFileWithOptions(
    os,
    path,
    OpenFileOptions{...DefaultOpenFileOptions(), Read: true}
  )
}

ReadFile = \(os Os, file File) string | error {
  r = if f = file.inner; stdIn {
    os.readStdIn()
  } else if stdOut {
    error("cannot read from stdout")
  } else if stdErr {
    error("cannot read from stderr")
  } else if file {
    os.readFile(f)
  }?

  if r.error == 0 {
    r.value
  } else {
    error(translateError(r.error))
  }
}

WriteFile = \(os Os, file File, data string) number | error {
  r = if f = file.inner; stdIn {
    error("cannot write to stdin")
  } else if stdOut {
    os.writeStdOut(data)
  } else if stdErr {
    os.writeStdErr(data)
  } else {
    os.writeFile(f, data)
  }?

  if r.error == 0 {
    r.count
  } else {
    error(translateError(r.error))
  }
}

CopyFile = \(os Os, src string, dest string) none | error {
  r = os.copyFile(src, dest)

  if r.error == 0 {
    none
  } else {
    error(translateError(r.error))
  }
}

RemoveFile = \(os Os, path string) none | error {
  r = os.removeFile(path)

  if r.error == 0 {
    none
  } else {
    error(translateError(r.error))
  }
}

ReadDirectory = \(os Os, path string) [string] | error {
  r = os.readDirectory(path)

  if r.error == 0 {
    convertArrayToStrings(r.paths)
  } else {
    error(translateError(r.error))
  }
}

CreateDirectory = \(os Os, path string) none | error {
  r = os.createDirectory(path)

  if r.error == 0 {
    none
  } else {
    error(translateError(r.error))
  }
}

RemoveDirectory = \(os Os, path string) none | error {
  r = os.removeDirectory(path)

  if r.error == 0 {
    none
  } else {
    error(translateError(r.error))
  }
}

Arguments = \(os Os) [string] {
  convertArrayToStrings(os.getArguments())
}

EnvironmentVariable = \(os Os, name string) string | error {
  r = os.getEnvironmentVariable(name)

  if r.error == 0 {
    r.value
  } else {
    error(translateError(r.error))
  }
}

convertArrayToStrings = \(array array) [string] {
  convertArrayToStringsFromIndex(array, 1)
}

convertArrayToStringsFromIndex = \(array array, index number) [string] {
  if index > _pen_ffi_array_length(array) {
    [string;]
  } else {
    [string;
      _pen_ffi_any_to_string(_pen_ffi_array_get(array, index)),
      ...convertArrayToStringsFromIndex(array, index + 1),
    ]
  }
}

translateError = \(code number) string {
  if code == 1 {
    "operation not permitted"
  } else if code == 2 {
    "no such file or directory"
  } else if code == 13 {
    "permission denied"
  } else if code == 17 {
    "file exists"
  } else if code == 20 {
    "not a directory"
  } else if code == 257 {
    "cannot lock file"
  } else if code == 258 {
    "cannot decode path as utf-8"
  } else if code == 259 {
    "environment variable not present"
  } else if code == 260 {
    "environment variable not unicode"
  } else {
    "unknown io error"
  }
}

export foreign _pen_os_main = \() number {
  _pen_main(
    Os{
      readStdIn: _pen_os_read_stdin,
      writeStdOut: _pen_os_write_stdout,
      writeStdErr: _pen_os_write_stderr,
      openFile: _pen_os_open_file,
      readFile: _pen_os_read_file,
      writeFile: _pen_os_write_file,
      copyFile: _pen_os_copy_file,
      removeFile: _pen_os_remove_file,
      readDirectory: _pen_os_read_directory,
      createDirectory: _pen_os_create_directory,
      removeDirectory: _pen_os_remove_directory,
      getArguments: _pen_os_get_arguments,
      getEnvironmentVariable: _pen_os_get_environment_variable,
    },
  )
}

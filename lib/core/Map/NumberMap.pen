import 'List
import 'Map
import 'Map'noValue { NoValue }
import 'Number

import foreign "c" _pen_core_hamt_hash_number \(number) number

type Map {
  map Map'Map
}

New = \() Map {
  Map{
    map: Map'New(
      \(x any, y any) boolean {
        if x = x as number {
          if y = y as number {
            x == y
          } else {
            false
          }
        } else {
          false
        }
      },
      \(x any) number {
        if x = x as number {
          _pen_core_hamt_hash_number(x)
        } else {
          0
        }
      },
    ),
  }
}

Get = \(map Map, key number, default any) any {
  if Number'IsNan(key) {
    default
  } else {
    Map'Get(map.map, key, default)
  }
}

Set = \(map Map, key number, value any) Map {
  if Number'IsNan(key) {
    map
  } else {
    Map{map: Map'Set(map.map, key, value)}
  }
}

Delete = \(map Map, key number) Map {
  if Number'IsNan(key) {
    map
  } else {
    Map{map: Map'Delete(map.map, key)}
  }
}

Size = \(map Map) number {
  Map'Size(map.map)
}

Merge = \(x Map, y Map) Map {
  Map{map: Map'Merge(x.map, y.map)}
}

Keys = \(map Map) [number] {
  List'ToNumbers(Map'Keys(map.map))
}

Values = \(map Map) [any] {
  Map'Values(map.map)
}

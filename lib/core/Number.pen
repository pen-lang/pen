import foreign "c" _pen_core_convert_number_to_string \(number) string
import foreign "c" _pen_core_power \(number, number) number
import foreign "c" _pen_core_remainder \(number, number) number
import foreign "c" _pen_core_square_root \(number) number
import foreign "c" _pen_core_is_nan \(number) boolean
import foreign "c" _pen_core_ceil \(number) number
import foreign "c" _pen_core_floor \(number) number
import foreign "c" _pen_core_nan \() number
import foreign "c" _pen_core_infinity \() number

String = \(x number) string {
  _pen_core_convert_number_to_string(x)
}

Sum = \(ns [number]) number {
  sumRecursive(ns, 0)
}

sumRecursive = \(ns [number], m number) number {
  if [n, ...ns] = ns {
    sumRecursive(ns, m + n())
  } else {
    m
  }
}

Remainder = \(x number, y number) number {
  _pen_core_remainder(x, y)
}

Power = \(x number, y number) number {
  _pen_core_power(x, y)
}

SquareRoot = \(x number) number {
  _pen_core_square_root(x)
}

Sequence = \(n number) [number] {
  Range(1, n)
}

Range = \(min number, max number) [number] {
  if min > max {
    [number]
  } else {
    [number min, ...Range(min + 1, max)]
  }
}

IsNan = \(x number) boolean {
  _pen_core_is_nan(x)
}

Ceil = \(x number) number {
  _pen_core_ceil(x)
}

Floor = \(x number) number {
  _pen_core_floor(x)
}

Nan = \() number {
  _pen_core_nan()
}

Infinity = \() number {
  _pen_core_infinity()
}

Minimum = \(x number, y number) number {
  if IsNan(x) | IsNan(y) {
    Nan()
  } else if x < y {
    x
  } else {
    y
  }
}

Maximum = \(x number, y number) number {
  if IsNan(x) | IsNan(y) {
    Nan()
  } else if x > y {
    x
  } else {
    y
  }
}

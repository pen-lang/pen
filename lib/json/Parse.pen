import Core'String
import Core'String'Byte
import Core'String'Byte'View { View }
import 'Value { Value }

type result {
  value Value
  input View
}

type stringResult {
  value string
  input View
}

result = \(v Value, i View) result {
  result{value: v, input: i}
}

stringResult = \(s string, i View) stringResult {
  stringResult{value: s, input: i}
}

Parse = \(s string) Value | error {
  r = value(View'New(s))?

  if View'Length(blanks(r.input)) == 0 {
    r.value
  } else {
    error("unexpected character")
  }
}

value = \(v View) result | error {
  v = blanks(v)

  if View'HasPrefix(v, "n") {
    null(v)
  } else if View'HasPrefix(v, "t") {
    true_(v)
  } else if View'HasPrefix(v, "f") {
    false_(v)
  } else if View'HasPrefix(v, "\"") {
    string_(v)
  } else {
    number_(v)
  }
}

null = \(v View) result | error {
  s = "null"

  if View'HasPrefix(v, s) {
    result(Value'New(none), View'Seek(v, Byte'Length(s)))
  } else {
    error("null expected")
  }
}

true_ = \(v View) result | error {
  s = "true"

  if View'HasPrefix(v, s) {
    result(Value'New(true), View'Seek(v, Byte'Length(s)))
  } else {
    error("true expected")
  }
}

false_ = \(v View) result | error {
  s = "false"

  if View'HasPrefix(v, s) {
    result(Value'New(false), View'Seek(v, Byte'Length(s)))
  } else {
    error("false expected")
  }
}

string_ = \(v View) result | error {
  r = stringCharacters(View'Seek(v, 1), [string])

  result(Value'New(r.value), View'Seek(r.input, 1))
}

stringCharacters = \(v View, ss [string]) stringResult {
  if r = stringCharacter(v) as none {
    stringResult(String'Join(ss, ""), v)
  } else {
    # TODO Optimize appending string elements.
    stringCharacters(r.input, [string ...ss, r.value])
  }
}

stringCharacter = \(v View) stringResult | none {
  if View'HasPrefix(v, "\\\"") {
    stringResult("\"", View'Seek(v, 2))
  } else if View'HasPrefix(v, "\"") {
    none
  } else {
    stringResult(Byte'Slice(View'ToString(v), 1, 1), View'Seek(v, 1))
  }
}

number_ = \(v View) result | error {
  # TODO
  error("not implemented")
}

blanks = \(v View) View {
  if vv = blank(v) as View {
    blanks(vv)
  } else {
    v
  }
}

blank = \(v View) View | none {
  if View'HasPrefix(v, " ") | View'HasPrefix(v, "\t") | View'HasPrefix(v, "\n") {
    View'Seek(v, 1)
  } else {
    none
  }
}
